# Copyright (c) 2023 SAP SE 
# Copyright (c) 2023 T-Systems International GmbH 
# Copyright (c) 2023 Contributors to the Eclipse Foundation
#
# See the NOTICE file(s) distributed with this work for additional
# information regarding copyright ownership.
#
# This program and the accompanying materials are made available under the
# terms of the Apache License, Version 2.0 which is available at
# https://www.apache.org/licenses/LICENSE-2.0.
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#
# SPDX-License-Identifier: Apache-2.0

PREFIX cx-common: <https://w3id.org/catenax/ontology/common#>
PREFIX cx-vehicle: <https://w3id.org/catenax/ontology/vehicle#>
PREFIX cx-bom: <https://w3id.org/catenax/ontology/bill-of-material#>
PREFIX cx-mat: <https://w3id.org/catenax/ontology/material#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

#
# A request for obtaining all asset administration shells for serialized parts
#

SELECT DISTINCT ?id ?pasp ?psasp ?hasRecycling ?slbomap ?slusap ?mfr WHERE {

    VALUES(?id) {
        (%s)
    }

    # all parts are twins
    ?id rdf:type cx-vehicle:Part.

    # Part
    OPTIONAL{
        ?id cx-vehicle:id ?manufacturerPartId.
        BIND(<urn:bamm:io.catenax.part_as_planned:1.0.1#PartAsPlanned> as ?pasp).
    }

    # PartSiteInformation
    OPTIONAL{
      ?id cx-vehicle:manufacturer ?partSite.
      BIND(<urn:bamm:io.catenax.part_site_information_as_planned:1.0.0#PartSiteInformationAsPlanned> as ?psasp).
    }

    # Single Level Bom
    OPTIONAL{
        ?id cx-bom:hasBom ?bom.
        BIND(<urn:bamm:io.catenax.single_level_bom_as_planned:2.0.0#SingleLevelBomAsPlanned> as ?slbomap).
        BIND(<urn:bamm:io.catenax.single_level_usage_as_planned:1.1.0#SingleLevelUsageAsPlanned> as ?slusap).
    } #bomAsPlanned

    # Material for Recycling
    OPTIONAL {
        ?id cx-mat:isMadeOf ?rMat.
        BIND(<urn:bamm:io.catenax.material_for_recycling:1.1.0#MaterialForRecycling> as ?mfr).
    }
}
ORDER BY DESC(?id)